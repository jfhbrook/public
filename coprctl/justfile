VERSION := "0.2.2"

# Lint with shellcheck
lint:
  shellcheck ./bin/coprctl

# Open a shell with bin in the PATH
shell:
  PATH="$(pwd)/bin:${PATH}" bash --login

# Build the Docker image
build:
  docker build -t 'jfhbrook/coprctl:{{VERSION}}' .
  docker tag 'jfhbrook/coprctl:{{VERSION}}' 'jfhbrook/coprctl:latest'

# Publish the Docker image to Docker Hub
publish:
  docker push 'jfhbrook/coprctl:{{VERSION}}'
  docker push jfhbrook/coprctl:latest

# Tag the release with tito
tag:
  tito tag --use-version '{{VERSION}}'

_push_tag:
  git push origin --follow-tags

# Apply COPR configuration
apply-copr:
  ./bin/coprctl apply -f ./package.yml

# Trigger a COPR build
build-copr: apply-copr
  copr build-package jfhbrook/joshiverse --name coprctl

# Tag and push a release with Tito and trigger a COPR build
release: tag _push_tag build-copr
