VERSION := "1.0.0"
PATCH := "1"

set dotenv-load := true

#
# Installing, updating and upgrading dependencies
#

_venv:
  if [ ! -d venv ]; then python3 -m venv venv; . ./venv/bin/activate && pip install pip wheel; fi

_clean-venv:
  rm -rf venv

# Install all dependencies
install:
  @just _venv
  . ./venv/bin/activate && pip install -r requirements_dev.txt

#
# Development
#

# Format Python with black
format:
  . ./venv/bin/activate &&  black ./bin/kbconfig

# Lint with flake8 and shellcheck
lint:
  . ./venv/bin/activate && flake8 './bin/kbconfig'
  shellcheck ./bin/*

# Open a shell with bins in the PATH
shell:
  . ./venv/bin/activate && PATH="$(pwd)/bin:${PATH}" bash --login

#
# Publishing
#

tag:
  . ./venv/bin/activate && tito tag --use-version '{{VERSION}}'

bundle:
  tar -czf korbenware-{{VERSION}}.tar.gz bin COPYING NOTICE README.md

push:
  git push origin --follow-tags

release:
  bash ./scripts/release.sh '{{VERSION}}' '{{PATCH}}'

apply-copr:
  if [[ "${OSTYPE}" == "darwin"* ]]; then echo "Skipping Copr apply on macOS"; else coprctl apply -f ./package.yml; fi

build-copr:
  if [[ "${OSTYPE}" == "darwin"* ]]; then echo "Visit https://copr.fedorainfracloud.org/coprs/jfhbrook/joshiverse/package/korbenware/ to build COPR package"; else copr build-package jfhbrook/joshiverse --name 'korbenware'; fi

apply-homebrew:
  bash ./scripts/apply-homebrew.sh '{{VERSION}}' '{{PATCH}}'

# Publish the release on GitHub, Homebrew, and Copr
publish: tag push bundle release apply-homebrew apply-copr build-copr
