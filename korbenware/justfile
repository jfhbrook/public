VERSION := "0.7.1"
PATCH := "1"

set dotenv-load := true

# Open a shell with bins in the PATH
shell:
  PATH="$(pwd)/bin:${PATH}" bash --login

tag:
  tito tag --use-version '{{VERSION}}'

bundle:
  tar -czf korbenware-{{VERSION}}.tar.gz bin COPYING NOTICE README.md

push:
  git push origin --follow-tags

release:
  bash ./scripts/release.sh '{{VERSION}}' '{{PATCH}}'

apply-copr:
  coprctl apply -f ./package.yml

build-copr:
  copr build-package jfhbrook/joshiverse --name 'korbenware'

apply-homebrew:
  bash ./scripts/apply-homebrew.sh '{{VERSION}}' '{{PATCH}}'

# Publish the release on GitHub, Homebrew, and Copr
publish: tag push bundle release apply-homebrew apply-copr build-copr
