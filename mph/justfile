set dotenv-load := true

tangle-directions := "open elisp.org in emacs and run C-c C-v to generate elisp snippets!"

elisp:
  if command -v org-tangle &> /dev/null; then org-tangle ./elisp.org || echo "warning, org-tangle failed - {{ tangle-directions }}"; else echo "warning, org-tangle not found - {{ tangle-directions }}"; fi

readme:
  pandoc -f org -t markdown README.org -o README.md

build: elisp readme
  cargo build

test: elisp readme
  cargo test

check: elisp readme
  cargo check

run *ARGS: elisp readme
  cargo run -- {{ ARGS }}

# Tag the release with tito
tag:
  tito tag --use-version "$(yq .package.version Cargo.toml)"

_push_tag:
  git push origin --follow-tags

# Apply copr configuration
apply-copr:
  coprctl apply -f ./package.yml

build-copr: apply-copr
  copr build-package jfhbrook/joshiverse --name 'korbenware'

_publish-cargo:
  cargo publish # note: --allow-dirty if you need it

publish: _publish-cargo tag _push_tag apply-copr build-copr
