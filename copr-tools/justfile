VERSION := "1.1.0"

set dotenv-load := true

# Lint with shellcheck
lint:
  shellcheck ./bin/*

# Open a shell with bin in the PATH
shell:
  PATH="$(pwd)/bin:${PATH}" bash --login

# Build the Docker image
build:
  docker build -t 'copr-tools:{{VERSION}}' .
  docker tag 'copr-tools:{{VERSION}}' 'copr-tools:latest'
  docker tag 'copr-tools:{{VERSION}}' 'jfhbrook/copr-tools:{{VERSION}}'
  docker tag 'jfhbrook/copr-tools:{{VERSION}}' 'jfhbrook/copr-tools:latest'

#
# Publishing
#

check_tap:
  if [ ! -d "${HOMEBREW_TAP}" ]; then echo "A HOMEBREW_TAP is required to publish."; exit 1; fi

tag:
  git tag 'copr-tools-{{VERSION}}' -m 'copr-tools release v{{VERSION}}'

bundle:
  tar -czf copr-tools-{{VERSION}}.tar.gz bin LICENSE README.md

push:
  git push origin --follow-tags

gh-release:
  bash ./scripts/release.sh '{{VERSION}}'

publish-docker:
  docker push 'jfhbrook/copr-tools:{{VERSION}}'
  docker push jfhbrook/copr-tools:latest

apply-homebrew:
  bash ./scripts/apply-homebrew.sh '{{VERSION}}'

# Publish the release on GitHub, Homebrew and Docker
publish: check_tap build tag push bundle gh-release publish-docker apply-homebrew
