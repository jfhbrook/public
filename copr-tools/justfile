VERSION := "0.3.0"
PATCH := "1"

set dotenv-load := true

# Lint with shellcheck
lint:
  shellcheck ./bin/coprctl

# Open a shell with bin in the PATH
shell:
  PATH="$(pwd)/bin:${PATH}" bash --login

# Build the Docker image
build-docker:
  docker build -t 'copr-tools:{{VERSION}}' .
  docker tag 'copr-tools:{{VERSION}}' 'copr-tools:latest'
  docker tag 'copr-tools:{{VERSION}}' 'jfhbrook/copr-tools:{{VERSION}}'
  docker tag 'jfhbrook/copr-tools:{{VERSION}}' 'jfhbrook/copr-tools:latest'

#
# Publishing
#

check_tap:
  if [ ! -d "${HOMEBREW_TAP}" ]; then echo "A HOMEBREW_TAP is required to publish."; exit 1; fi

tag:
  tito tag --use-version '{{VERSION}}'

bundle:
  tar -czf copr-tools-{{VERSION}}.tar.gz bin LICENSE README.md

push:
  git push origin --follow-tags

gh-release:
  bash ./scripts/release.sh '{{VERSION}}' '{{PATCH}}'

publish-docker:
  docker push 'jfhbrook/copr-tools:{{VERSION}}'
  docker push jfhbrook/copr-tools:latest

apply-homebrew:
  bash ./scripts/apply-homebrew.sh '{{VERSION}}' '{{PATCH}}'

apply-copr:
  coprctl apply -f ./coprctl.yml

build-copr:
  copr build-package jfhbrook/joshiverse --name 'coprctl'

# Publish the release on GitHub, Homebrew, Docker and Copr
publish: check_tap tag push bundle gh-release apply-homebrew publish-docker apply-copr build-copr
